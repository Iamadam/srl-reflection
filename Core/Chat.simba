//------------------------------------------------------------------\\
//--               Simba Reflection Library                       --||
//--                     Chat Routines                            --||
//------------------------------------------------------------------//

//---                               Constants                               --//
const
  CHAT_TYPE_NORMAL  = 0;
  CHAT_TYPE_CHATTER = 1;
  //  Private
  CHAT_TYPE_PRIVATE = 2;
  //  Trade
  CHAT_TYPE_TRADE   = 3;
  //  Black/System
  CHAT_TYPE_SYSTEM  = 4;
  //  Includes all the former Types
  CHAT_TYPE_ALL     = 5;


{*******************************************************************************
function GetChatMessages(ChatType: string; Visible, NoName: Boolean): TStringArray;
By: lordsaturn
Description: Gets all chat messages using interfaces according to params.
Valid ChatType Params:
  'chat', 'normal':   Normal chat
  'pm', 'private':    Private messages
  'trade':            Trade chat
  'black', 'message': Black chat messages
  'all':              All of the above
*******************************************************************************}

function GetChatMessages(ChatType: Integer; Visible, NoName: Boolean): TStringArray;
var
  I, L: integer;
  FindStr, tmp, s: string;
begin
  Result := [];
  case (ChatType) of
    CHAT_TYPE_NORMAL: FindStr := '<col=0000ff>';
    CHAT_TYPE_CHATTER: FindStr := '<col=800000>';
    CHAT_TYPE_PRIVATE: FindStr := '<col=800080>';
    CHAT_TYPE_SYSTEM: FindStr := 'blk';
    CHAT_TYPE_ALL: FindStr := 'all';
    else
    begin
      srl_Warn('GetChatMessages', ToStr(ChatType)+' is not a valid chat type.',
               warn_AllVersions);
      Exit;
    end;
  end;
  SetArrayLength(Result, 102);
  for I := 279 downto 180 do
  begin
    s := GetInterfaceText(137, I);
    if (s <> '') and ((Pos(FindStr, s) <> 0) or (FindStr = 'blk') or (FindStr = 'all')) then
    begin
      s := Replace(s, '<img=3>', ''); //quick chat
      Tmp := s;
      s := TrimHTML(s);
      if ((FindStr = 'blk') and (Tmp <> s)) then Continue;
      if NoName and (FindStr = '<col=0000ff>') then
        Delete(s, 1, Pos(':', s)+1);
      Result[L] := s;
      Inc(L);
    end;
  end;
  if Visible then
    if L > 8 then
      L := 8;
  SetArrayLength(Result, L);
end;

{*******************************************************************************
function ChatBoxTextExists(ChatType: Integer; Text: string): Boolean;
By: lordsaturn
Description: Returns true if text exists and is visible in the chatbox according
to 'ChatType'.
*******************************************************************************}
function ChatBoxTextExists(ChatType: Integer; Text: string): Boolean;
var
  s : TStringArray;
  I, H : integer;
begin
  S := GetChatMessages(ChatType, True, True);
  H := High(S);
  for I := 0 to H do
  begin
    Result := (Pos(Text, S[I]) <> 0);
    if Result then Exit;
  end;
end;

{*******************************************************************************
function GetChatBoxLine(Line: integer): string;
By: lordsaturn
Description: Returns the text in the given line of the chatbox (1 = top, 8 = bottom)
*******************************************************************************}
function GetChatBoxLine(Line: integer): string;

var
  S : TStringArray;

begin
  if (Line < 1) or (Line > 8) then
  begin
    srl_Warn('GetChatBoxLine', 'The paramater ''Line'' must be between 1 and 8.', warn_AllVersions);
    Exit;
  end;
  s := GetChatMessages(CHAT_TYPE_ALL, True, False);
  Result := s[8-line];
end;

{*******************************************************************************
function LastChatMessage(ChatType: Integer): string;
By: lordsaturn
Description: Gets last chat message loaded according to 'ChatType'. (see
GetChatMessages for a list of chat types)
*******************************************************************************}
function LastChatMessage(ChatType: Integer): string;

var
  S : TStringArray;

begin
  S := GetChatMessages(ChatType, False, False);
  try
    Result := s[0];
  except end;
end;

{*******************************************************************************
function R_LastChatter: string;
By: lordsaturn
Description: Gets the last loaded player to talk.
*******************************************************************************}
function R_LastChatter: string;
var
  S : TStringArray;
  P : integer;
begin
  S := GetChatMessages(CHAT_TYPE_CHATTER, False, False);
  try
    Result := S[0];
  except
    Result := '';
    Exit;
  end;
  P := Pos(':', Result);
  if P <> 0 then
    Delete(Result, P, Length(Result) - P + 1);
end;

{*******************************************************************************
function FindNPCText(text: string): boolean;
By: Drags111
Description: Searches through the known talking interfaces to search for the
            inputed text.
*******************************************************************************}
function FindNPCText(text: string): boolean;
var
  inter, i, z, L: integer;
  str: String;
begin
  Result := False;
  for i := 0 to High(INTERFACE_TALKS) do
  begin
    inter := SmartGetFieldArrayObject(0, hook_static_GetInterfaces, INTERFACE_TALKS[i]);
    L := SmartGetFieldArraySize(inter, '', 1);
    SmartFreeObject(inter);

    for z := 0 to L-1 do
    begin
      str := GetInterfaceText(INTERFACE_TALKS[i], z);
      //Writeln(str);
      if(pos(lowercase(trim(text)), lowercase(trim(str))) > 0)then
      begin
        Writeln('Found text at interface '+ToStr(INTERFACE_TALKS[i])+', '+ToStr(z));
        Result := True;
        Exit;
      end;
    end;
  end;
end;

{*******************************************************************************
function R_ClickToContinue: boolean;
By: Drags111
Description: Does as the name suggests. Clicks the 'click here to continue' crap
*******************************************************************************}
function R_ClickToContinue: boolean;
var
  inter, i, z, L, x, y, w, h: integer;
  str: String;
begin
  Result := False;
  for i := 0 to High(INTERFACE_TALKS) do
  begin
    inter := SmartGetFieldArrayObject(0, hook_static_GetInterfaces, INTERFACE_TALKS[i]);
    L := SmartGetFieldArraySize(inter, '', 1);
    SmartFreeObject(inter);

    for z := 0 to L-1 do
    begin
      str := GetInterfaceText(INTERFACE_TALKS[i], z);
      if(pos(lowercase(trim('click here to conti')), lowercase(trim(str))) > 0)then
      begin
        //Writeln('Found CTC at interface '+ToStr(INTERFACE_TALKS[i])+', '+ToStr(z));
        inter := GetInterface(INTERFACE_TALKS[i], z);
        x := SmartGetFieldInt(inter, hook_interface_GetX) + 7;
        y := SmartGetFieldInt(inter, hook_interface_GetY) + 347;
        w := SmartGetFieldInt(inter, hook_interface_GetWidth);
        h := SmartGetFieldInt(inter, hook_interface_GetHeight);
        MouseBox(x, y, x+w, y+h, 1);
        SmartFreeObject(inter);
        Result := true;
        Exit;
      end;
    end;
  end;
end;
